# Determine platform- and compiler-specific settings

# check for and activate c++11 support
include(${PROJECT_SOURCE_DIR}/cmake/CXX11.cmake)
check_for_cxx11_compiler(CXX11_COMPILER)
if(CXX11_COMPILER)
  enable_cxx11()
  ADD_DEFINITIONS("-DCPP11=1")
else(CXX11_COMPILER)
  MESSAGE(FATAL_ERROR "Your C++ compiler version (${CMAKE_CXX_COMPILER_ID} ${COMPILER_VERSION}) is not compatible with C++11. Please use a fully C++11 compliant compiler -- see the EUDAQ documentation for more information.")
endif(CXX11_COMPILER)

# platform dependent preprocessor defines
if (WIN32)
  if (CYGWIN)
    ADD_DEFINITIONS("-DEUDAQ_PLATFORM=PF_CYGWIN")
  elseif (MINGW)
    ADD_DEFINITIONS("-DEUDAQ_PLATFORM=PF_MINGW")
  else()
    ADD_DEFINITIONS("-DEUDAQ_PLATFORM=PF_WIN32")
  endif()
elseif (UNIX)
  if (${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
    ADD_DEFINITIONS("-DEUDAQ_PLATFORM=PF_MACOSX")
  else()
    ADD_DEFINITIONS("-DEUDAQ_PLATFORM=PF_LINUX")
  endif()
else()
  MESSAGE(WARNING "WARNING: Platform not defined in CMakeLists.txt -- assuming Unix/Linux (good luck)." )
  ADD_DEFINITIONS("-DEUDAQ_PLATFORM=PF_LINUX")
endif()

# determine compiler type (32/64bit)
if( CMAKE_SIZEOF_VOID_P EQUAL 8 )
#    MESSAGE(STATUS "64-bit compiler detected" )
    SET( EX_PLATFORM 64 )
    SET( EX_PLATFORM_NAME "x64" )
else( CMAKE_SIZEOF_VOID_P EQUAL 8 ) 
#    MESSAGE(STATUS "32-bit compiler detected" )
    SET( EX_PLATFORM 32 )
    SET( EX_PLATFORM_NAME "x86" )
endif( CMAKE_SIZEOF_VOID_P EQUAL 8 )

# compiler specific settings
if (CMAKE_COMPILER_IS_GNUCC)
   # add some more general preprocessor defines (only for gcc)
   message(STATUS "Using gcc-specific preprocessor identifiers")
   add_definitions("-DEUDAQ_FUNC=__PRETTY_FUNCTION__")
elseif(MSVC)
   message(STATUS "Using MSVC-specific preprocessor identifiers and options")
   add_definitions("-DEUDAQ_FUNC=__FUNCTION__")
   add_definitions("/wd4251") # disables warning concerning dll-interface (comes up for std classes too often)
   add_definitions("/wd4996") # this compiler warnung is about that functions like fopen are unsafe.
else()
   add_definitions("-DEUDAQ_FUNC=__func__")
endif()
